// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  ext.kotlin_version = "1.7.10"
  ext.hilt_version = '2.42'
  ext.ktlint_version = '0.45.2'

  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath "com.android.tools.build:gradle:7.2.1"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.7.2"

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter() // Warning: this repository is going to shut down soon
    maven { url 'https://jitpack.io' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
  }
}

final Set<String> EDITOR_CONFIG_KEYS = [
  "ij_kotlin_imports_layout",
  "indent_size",
  "end_of_line",
  "charset",
  "continuation_indent_size"
]

subprojects {
  apply plugin: "com.diffplug.spotless"

  spotless {
    kotlin {
      target("**/*.kt")

      // TODO this should all come from editorconfig https://github.com/diffplug/spotless/issues/142
      final data = [
        "indent_size"             : "2",
        "continuation_indent_size": "4",
        "ij_kotlin_imports_layout": "*",
        "end_of_line"             : "lf",
        "charset"                 : "utf-8",
        "disabled_rules"          : "experimental:package-name,experimental:trailing-comma",
      ]

      ktlint(ktlint_version)
        .setUseExperimental(true)
        .userData(data.subMap(data.keySet() - EDITOR_CONFIG_KEYS))
        .editorConfigOverride(data.subMap(EDITOR_CONFIG_KEYS))

      trimTrailingWhitespace()
      indentWithSpaces()
      endWithNewline()
    }

    format("xml") {
      target("**/res/**/*.xml")

      trimTrailingWhitespace()
      indentWithSpaces()
      endWithNewline()
    }

    groovyGradle {
      target("*.gradle")

      trimTrailingWhitespace()
      indentWithSpaces()
      endWithNewline()
    }

    kotlinGradle {
      target("**/*.gradle.kts", "*.gradle.kts")

      // TODO this should all come from editorconfig https://github.com/diffplug/spotless/issues/142
      final data = [
        "indent_size"             : "2",
        "continuation_indent_size": "4",
        "ij_kotlin_imports_layout": "*",
        "end_of_line"             : "lf",
        "charset"                 : "utf-8",
        "disabled_rules"          : "no-wildcard-imports",
      ]

      ktlint(ktlint_version)
        .setUseExperimental(true)
        .userData(data.subMap(data.keySet() - EDITOR_CONFIG_KEYS))
        .editorConfigOverride(data.subMap(EDITOR_CONFIG_KEYS))

      trimTrailingWhitespace()
      indentWithSpaces()
      endWithNewline()
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
