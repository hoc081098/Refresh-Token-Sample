plugins {
  id 'com.android.application'
  id 'kotlin-android'
  id 'kotlin-kapt'
  id "com.google.protobuf" version "0.8.19"
  id "dagger.hilt.android.plugin"
}

android {
  compileSdkVersion 32
  buildToolsVersion "32.0.0"

  defaultConfig {
    applicationId "com.hoc081098.refreshtokensample"
    minSdkVersion 23
    targetSdkVersion 32
    final MAJOR = 1
    final MINOR = 2
    final PATCH = 0
    versionCode MAJOR * 10000 + MINOR * 100 + PATCH
    versionName "$MAJOR.$MINOR.$PATCH"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }

  buildFeatures {
    viewBinding true
  }
}

dependencies {
  implementation 'androidx.core:core-ktx:1.8.0'
  implementation 'androidx.appcompat:appcompat:1.4.2'
  implementation 'com.google.android.material:material:1.6.1'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
  implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
  implementation("androidx.activity:activity-ktx:1.4.0")

  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

  implementation 'com.squareup.moshi:moshi-kotlin:1.13.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.9'
  implementation 'com.squareup.retrofit2:retrofit:2.9.0'
  implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.3'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.2'

  implementation 'androidx.datastore:datastore:1.0.0'
  implementation 'com.google.protobuf:protobuf-javalite:3.21.2'
  implementation "com.google.crypto.tink:tink-android:1.6.1"

  implementation "com.google.dagger:hilt-android:$hilt_version"
  kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

  implementation 'com.github.hoc081098:ViewBindingDelegate:1.3.1'
  implementation 'io.github.hoc081098:FlowExt:0.4.0-SNAPSHOT'
  implementation 'com.jakewharton.timber:timber:5.0.1'
}

kapt {
  javacOptions {
    // These options are normally set automatically via the Hilt Gradle plugin, but we
    // set them manually to workaround a bug in the Kotlin 1.5.20
    option("-Adagger.fastInit=ENABLED")
    option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.21.2"
  }

  // Generates the java Protobuf-lite code for the Protobufs in this project. See
  // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
  // for more information.
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {
          option 'lite'
        }
      }
    }
  }
}
